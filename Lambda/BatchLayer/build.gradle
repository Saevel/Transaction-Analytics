group 'org.dmcs'

version '1.0'

apply plugin: 'scala'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/it/scala')
        }
        resources.srcDir file('src/it/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {

    compile project(':Events')
    compile project(':Model')

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.8'
    compile group: 'org.apache.spark', name: 'spark-sql_2.11', version: '1.6.2'

    testCompile group: 'org.scalacheck', name: 'scalacheck_2.11', version: '1.12.5'
    testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: '2.2.6'

    integrationTestCompile group: 'io.spray', name: 'spray-client', version: '1.3.1'
}

task it(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn it
it.mustRunAfter test