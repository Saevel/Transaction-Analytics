group 'org.dmcs'

version '1.0'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.8'
    }
}

project.ext.set("configs", "batch,spark")

apply plugin: 'scala'

apply plugin: 'gradle-one-jar'

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import org.dmcs.transaction.analytics.mesosphere.MesosphereDeployment

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/it/scala')
        }
        resources.srcDir file('src/it/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

// TODO: Fix Spark provided dependencies.
// TODO: Port to Akka HTTP?
dependencies {

    compile project(':Events')
    compile project(':Model')

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.8'
    compile group: 'org.apache.spark', name: 'spark-sql_2.11', version: '1.6.0'

    compile group: 'io.spray', name: 'spray-can_2.11', version: '1.3.3'
    compile group: 'io.spray', name: 'spray-routing_2.11', version: '1.3.3'
    compile group: 'io.spray', name: 'spray-http_2.11', version: '1.3.3'

    testCompile group: 'org.scalacheck', name: 'scalacheck_2.11', version: '1.13.2'
    testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: '2.2.6'

    integrationTestCompile group: 'io.spray', name: 'spray-client', version: '1.3.1'
}

// TODO: Get these from envs
docker {
    url=System.getenv('DOCKER_MACHINE_URL')
    certPath=file(System.getenv('DOCKER_CERT_PATH'))
}

task fatJar(type: OneJar){
    mainClass = 'org.dmcs.transaction.analytics.lambda.speed.layer.SpeedLayerApplication'
}

/**
 * Integration tests.
 */
task it(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

/**
 * Copies config from the top-level project into this module.
 */
task getConfigs(type: Copy){
    if(project.hasProperty('configs')){
        configs.split(",").each{ config ->
            from(rootProject.projectDir.getAbsolutePath() + "/src/main/resources/" + config + ".properties")
            into(project.buildDir.getAbsolutePath() + "/resources/main")
        }
    }
}

task prepareDockerBuild(type: Copy) {
    from(project.buildDir.getAbsolutePath() + "/libs/" + project.name + "-" + project.version + "-standalone.jar")
    into(project.buildDir.getAbsolutePath() + "/docker")

    from(project.projectDir.getAbsolutePath() + "/src/main/docker")
    into(project.buildDir.getAbsolutePath() + "/docker")
}

task dockerBuild(type: DockerBuildImage){
    inputDir = project.file(project.buildDir.path + "/docker")
    tag = System.getenv("DOCKER_USERNAME") + "/transaction-analytics-lambda-speed-layer"
}

task dockerize(type: DockerPushImage){
    imageName = System.getenv("DOCKER_USERNAME") + "/transaction-analytics-lambda-speed-layer"
}

// TODO: Implement!
task deploy {
    logger.debug 'DCOS Deployment: Deploy SpeedLayer to Marathon'
    // MesosphereDeployment.deployToMarathon("", null);
}

// Task dependencies configuration
deploy.dependsOn dockerize
dockerize.dependsOn dockerBuild
dockerBuild.dependsOn prepareDockerBuild
prepareDockerBuild.dependsOn fatJar
fatJar.dependsOn build
compileScala.dependsOn getConfigs
check.dependsOn it
it.mustRunAfter test