group 'org.dmcs'

version '1.0'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.8'
    }
}

apply plugin: 'scala'

apply plugin: 'gradle-one-jar'

apply plugin: 'groovy'

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import org.dmcs.transaction.analytics.mesosphere.MesosphereDeployment
import org.dmcs.transaction.analytics.mesosphere.marathon.MarathonApplication
import org.dmcs.transaction.analytics.mesosphere.marathon.PortDefinition

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    acceptanceTestCompile{} extendsFrom compile
    acceptanceTestRuntime{} extendsFrom runtime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/it/scala')
        }
        resources.srcDir file('src/it/resources')
    }

    acceptanceTests {
        java {
            compileClasspath += main.output + configurations.acceptanceTestCompile
            runtimeClasspath += main.output + configurations.acceptanceTestRuntime
            srcDir file('src/acceptance/scala')
        }
        resources.srcDir file('src/acceptance/resources')
    }
}


task deploy {
    String marathonUrl = System.getenv("DCOS_URL") + "/service/marathon-user";
    // TODO: Marathon Application!
    /*
    MesosphereDeployment.deployToMarathon(marathonUrl, new MarathonApplication(
       "Classical", "java -jar /usr/src/Classical-1.0-standalone.jar", "0.5", "256",
            Arrays.asList(new PortDefinition(8080, "http", "http", null)), true, 1,
    ))
    */
}

dependencies {

    compile group: 'org.scala-lang', name: 'scala-library', version: '2.11.8'
    compile group: 'com.outworkers', name: 'phantom-streams_2.11', version: '2.0.0'
    compile group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.1'
    compile group: 'org.typelevel', name: 'cats-core_2.11', version: '0.9.0'

    compile project(':Model')
    compile project(':Events')

    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: '3.0.0'
    testCompile group: 'org.scalacheck', name: 'scalacheck_2.11', version: '1.12.6'

    acceptanceTestCompile group: 'org.scala-lang', name: 'scala-library', version: '2.11.8'
    acceptanceTestCompile project(':TestFramework')
}

docker {
    url=System.getenv('DOCKER_MACHINE_URL')
    certPath=file(System.getenv('DOCKER_CERT_PATH'))

    registryCredentials {
        url = System.getenv("DOCKER_REGISTRY_URL")
        username = System.getenv("DOCKER_USERNAME")
        password = System.getenv("DOCKER_PASSWORD")
        email = System.getenv("DOCKER_USER_EMAIL")
    }
}

task fatJar(type: OneJar){
    mainClass = 'org.dmcs.transaction.analytics.classical.ClassicalAnalyticsApplication'
}

task it(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task acceptanceTestCompile(type: ScalaCompile) {
    source = fileTree(dir: 'src/acceptance/scala', include: '**/*.scala')
    destinationDir = file('build/classes/acceptance')
    classpath = sourceSets.acceptanceTests.runtimeClasspath
    scalaCompileOptions.incrementalOptions.analysisFile = new File("${buildDir}/tmp/scala/compilerAnalysis/compileCustomScala.analysis")
}

task acceptanceTestBaseJar(type: Jar){
    archiveName = 'Classical-Acceptance-Tests-partial.jar'
    manifest {
        attributes(
            'Main-Class':'org.dmcs.transaction.analytics.classical.ClassicalAcceptanceTestsApplication',
            'Class-Path': configurations.acceptanceTestCompile.files.collect {"$it.name"}.join(' ')
        )
    }
    from 'build/classes/acceptance'
}

task acceptanceTestJar(type: OneJar) {
    mainClass = 'org.dmcs.transaction.analytics.classical.ClassicalAcceptanceTestsApplication'
    baseJar = acceptanceTestBaseJar
    archiveName = 'Classical-Acceptance-Tests.jar'
}

acceptanceTestJar.dependsOn(acceptanceTestBaseJar)

task prepareAcceptanceTestDockerBuild(type: Copy) {
    from(project.buildDir.getAbsolutePath() + "/libs/Classical-Acceptance-Tests.jar")
    into(project.buildDir.getAbsolutePath() + "/acceptance/docker")

    from(project.projectDir.getAbsolutePath() + "/src/acceptance/docker")
    into(project.buildDir.getAbsolutePath() + "/acceptance/docker")
}

task buildAcceptanceTestDocker(type: DockerBuildImage) {
    inputDir = project.file(project.buildDir.path + "/acceptance/docker")
    tag = System.getenv("DOCKER_USERNAME") + "/transaction-analytics-classical-acceptance-tests"
}

task acceptanceTestDockerize(type: DockerPushImage) {
    imageName = System.getenv("DOCKER_USERNAME") + "/transaction-analytics-classical-acceptance-tests"
}

// TODO: Implement
task acceptanceTest {
    // TODO: Deploy acceptance tests to Maraton.
    // String marathonUrl = System.getenv("DCOS_URL") + "/service/marathon-user";
    logger.info("Acceptance Tests for the Classical project")
    /*
    MesosphereDeployment.deployToMarathon(marathonUrl, new MarathonApplication(
            "Classical", "java -jar /usr/src/Classical-1.0-standalone.jar", "0.5", "256",
            Arrays.asList(new PortDefinition(8080, "http", "http", null)), true, 1,
    ))
    */
}

acceptanceTestBaseJar.dependsOn(acceptanceTestCompile)
prepareAcceptanceTestDockerBuild.dependsOn(acceptanceTestJar)
buildAcceptanceTestDocker.dependsOn(prepareAcceptanceTestDockerBuild)
acceptanceTestDockerize.dependsOn(buildAcceptanceTestDocker)
acceptanceTest.dependsOn(acceptanceTestDockerize)

task prepareDockerBuild(type: Copy) {
    from(project.buildDir.getAbsolutePath() + "/libs/" + project.name + "-" + project.version + "-standalone.jar")
    into(project.buildDir.getAbsolutePath() + "/docker")

    from(project.projectDir.getAbsolutePath() + "/src/main/docker")
    into(project.buildDir.getAbsolutePath() + "/docker")
}

task dockerBuild(type: DockerBuildImage){
    inputDir = project.file(project.buildDir.path + "/docker")
    tag = System.getenv("DOCKER_USERNAME") + "/transaction-analytics-classical"
}

task dockerize(type: DockerPushImage){
    imageName = System.getenv("DOCKER_USERNAME") + '/transaction-analytics-classical'
}

acceptanceTest.dependsOn deploy
deploy.dependsOn dockerize
dockerize.dependsOn dockerBuild
dockerBuild.dependsOn prepareDockerBuild
prepareDockerBuild.dependsOn fatJar
fatJar.dependsOn build
check.dependsOn it
it.mustRunAfter test